apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/Rairmmd/AndMvp'
def gitUrl = 'https://github.com/Rairmmd/AndMvp.git'
group = "com.rairmmd"
version = "1.0.2"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
    }

    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.android.support:appcompat-v7:28.0.0'
    //Rx
    api 'io.reactivex.rxjava2:rxjava:2.2.2'
    api 'io.reactivex.rxjava2:rxandroid:2.1.0'
    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.4.0'
    api 'com.squareup.retrofit2:converter-scalars:2.4.0'
    //权限
    api 'com.yanzhenjie:permission:1.1.2'
    //ButterKnife
    api 'com.jakewharton:butterknife:8.8.1'
    //Dialog
//    api 'com.afollestad.material-dialogs:core:0.9.6.0'
    //LoadingDialog
    api 'com.github.gittjy:LoadingDialog:1.0.2'
    //Toasty
    api 'com.github.GrenderG:Toasty:1.3.1'
    //Klog
    api 'com.github.zhaokaiqiang.klog:library:1.6.0'
    //fragmentation
    api 'me.yokeyword:fragmentation:1.3.6'
    //状态栏
    api 'com.gyf.barlibrary:barlibrary:2.3.0'
    //鲁班图片压缩
    api 'top.zibin:Luban:1.1.8'
    //Picasso
    api 'com.squareup.picasso:picasso:2.71828'
    //图片选择
    api 'com.yanzhenjie:album:2.1.3'
    //适配器
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    //EventBus
    api "org.greenrobot:eventbus:3.1.1"
    //侧滑退出
    api 'com.r0adkll:slidableactivity:2.0.6'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'AndMvp'
                description '轻量级的Android MVP快速开发框架'
                url siteUrl
                //开源协议
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'rairmmd'
                        name 'rairmmd'
                        email 'rairmmd@qq.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
//配置源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//配置文档
task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        //仓库
        repo = "maven"
        //仓库名
        name = "AndMvp"
        // #CONFIG# project name in jcenter
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
